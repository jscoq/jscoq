<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <meta http-equiv="content-type" content="text/html;charset=utf-8" />

    <link rel="stylesheet" href="../node_modules/bootstrap/dist/css/bootstrap.min.css" />

    <title>Use Coq in Your Browser: The Js Coq Theorem Prover Online IDE!</title>
  </head>

<body>
  <div id="ide-wrapper" class="toggled">
  <div id="code-wrapper">
  <div id="document">

<div><p>
 <tt>Equations</tt> is a plugin for  that comes with a few support modules defining
   classes and tactics for running it. We will introduce its main
   features through a handful of examples. We start our Coq primer
   session by importing the <tt>Equations</tt> module.  </div>
<div><textarea id='coq-ta-2'>
From Coq Require Import Arith.
From Coq Require Import Omega.
From Equations Require Import Equations.</textarea></div>
Set Equations Debug.
<div><p>
<h1>
 Inductive types
</h1>

<p>
   In its simplest form, <tt>Equations</tt> allows to define functions on inductive datatypes.
   Take for example the booleans defined as an inductive type with two constructors <tt>true</tt> and <tt>false</tt>:
   <pre class="inline-coq" data-lang="coq">
   Inductive bool : Set := true : bool | false : bool <p>
</pre>   
   We can define the boolean negation as follows: </div>
<div><textarea id='coq-ta-3'>
Equations neg (b : bool) : bool :=
neg true := false ;
neg false := true.
Print All.
</textarea></div>
<div><p>
<tt>Equations</tt> declarations are formed by a signature definition and a set of <em>clauses</em> 
   that must form a <em>covering</em> of this signature. The compiler is then expected to
   automatically find a corresponding case-splitting tree that implements the function.
   In this case, it simply needs to split on the single variable <tt>b</tt> to
   produce two new <em>programming problems</em> <tt>neg true</tt> and <tt>neg false</tt> that are directly 
   handled by the user clauses. We will see in more complex examples that this search
   for a splitting tree may be non-trivial. <p>
<h1>
 Reasoning principles
</h1>

<p>
   In the setting of a proof assistant like Coq, we need not only the ability 
   to define complex functions but also get good reasoning support for them.
   Practically, this translates to the ability to simplify applications of functions 
   appearing in the goal and to give strong enough proof principles for (recursive)
   definitions.
<p>
   <tt>Equations</tt> provides this through an automatic generation of proofs related to
   the function. Namely, each defining equation gives rise to a lemma stating the 
   equality between the left and right hand sides. These equations can be used as 
   rewrite rules for simplification during proofs, without having to rely on the
   fragile simplifications implemented by raw reduction. We can also generate the
   inductive graph of any <tt>Equations</tt> definition, giving the strongest elimination
   principle on the function. 
<p>
   I.e., for <tt>neg</tt> the inductive graph is defined as: <pre class="inline-coq" data-lang="coq">
Inductive neg_ind : bool -> bool -> Prop :=
| neg_ind_equation_1 : neg_ind true false
| neg_ind_equation_2 : neg_ind false true <p>
</pre>
   Along with a proof of <tt>Î  b, neg_ind b (neg b)</tt>, we can eliminate any call
   to <tt>neg</tt> specializing its argument and result in a single command. 
   Suppose we want to show that <tt>neg</tt> is involutive for example, our goal will 
   look like: <pre class="inline-coq" data-lang="coq">
  b : bool
  ============================
   neg (neg b) = b <p>
</pre>   An application of the tactic <tt>funelim (neg b)</tt> will produce two goals corresponding to 
   the splitting done in <tt>neg</tt>: <tt>neg false = true</tt> and <tt>neg true = false</tt>.
   These correspond exactly to the rewriting lemmas generated for <tt>neg</tt>.
<p>
   In the following sections we will show how these ideas generalize to more complex 
   types and definitions involving dependencies, overlapping clauses and recursion.
<p>
<h1>
 Building up
</h1>

<p>
<h2>
 Polymorphism
</h2>

<p>
   Coq's inductive types can be parameterized by types, giving polymorphic datatypes.
   For example the list datatype is defined as:
   </div>
<div><textarea id='coq-ta-4'>

Inductive list {A} : Type := nil : list | cons : A -> list -> list.

Arguments list : clear implicits.
Notation "x :: l" := (cons x l).

</textarea></div>
<div><p>
No special support for polymorphism is needed, as type arguments are treated 
   like regular arguments in dependent type theories. Note however that one cannot
   match on type arguments, there is no intensional type analysis.
   We can write the polymorphic <tt>tail</tt> function as follows:
</div>
<div><textarea id='coq-ta-5'>

Equations tail {A} (l : list A) : list A :=
tail nil := nil ;
tail (cons a v) := v.

</textarea></div>
<div><p>
Note that the argument <tt>{A}</tt> is declared implicit and must hence be
 omitted in the defining clauses. In each of the branches it is named
 <tt>A</tt>. To specify it explicitely one can use the syntax <tt>{A:=B}</tt>,
 renaming that implicit argument to <tt>B</tt> in this particular case <p>
<h2>
 Recursive inductive types
</h2>

<p>
   Of course with inductive types comes recursion. Coq accepts a subset
   of the structurally recursive definitions by default (it is
   incomplete due to its syntactic nature). We will use this as a first
   step towards a more robust treatment of recursion via well-founded
   relations. A classical example is list concatenation: </div>
<div><textarea id='coq-ta-6'>

Equations app {A} (l l' : list A) : list A :=
app nil l' := l' ;
app (cons a l) l' := cons a (app l l').

</textarea></div>
<div><p>
Recursive definitions like <tt>app</tt> can be unfolded easily so proving the 
   equations as rewrite rules is direct. The induction principle associated 
   to this definition is more interesting however. We can derive from it the 
   following <em>elimination</em> principle for calls to <tt>app</tt>: <pre class="inline-coq" data-lang="coq">
   app_elim :
   forall P : forall (A : Type) (l l' : list A), list A -> Prop,
   (forall (A : Type) (l' : list A), P A nil l' l') ->
   (forall (A : Type) (a : A) (l l' : list A),
   P A l l' (app l l') -> P A (a :: l) l' (a :: app l l')) ->
   forall (A : Type) (l l' : list A), P A l l' (app l l') <p>
</pre>  Using this eliminator, we can write proofs exactly following the 
  structure of the function definition, instead of redoing the splitting 
  by hand. This idea is already present in the <tt>Function</tt> package 
   that derives induction principles from
  function definitions.
 </div>
<div><textarea id='coq-ta-7'>


</textarea></div>
<div><p>
<h2>
 Moving to the left
</h2>

<p>
   The structure of real programs is richer than a simple case tree on
   the original arguments in general. In the course of a computation, we
   might want to scrutinize intermediate results (e.g. coming from
   function calls) to produce an answer. This literally means adding a
   new pattern to the left of our equations made available for further
   refinement. This concept is know as with clauses in the Agda
    community and was first presented and
   implemented in the Epigram language
   .
<p>
   The compilation of with clauses and its treatment for generating
   equations and the induction principle are quite involved in the
   presence of dependencies, but the basic idea is to add a new case
   analysis to the program. To compute the type of the new subprogram,
   we actually abstract the discriminee term from the expected type of
   the clause, so that the type can get refined in the subprogram. In
   the non-dependent case this does not change anything though.
<p>
   Each <tt>with</tt> node generates an auxiliary definition from the clauses
   in the curly brackets, taking the additional object as argument. The
   equation for the with node will simply be an indirection to the
   auxiliary definition and simplification will continue as usual with
   the auxiliary definition's rewrite rules.  </div>
<div><textarea id='coq-ta-8'>

Equations filter {A} (l : list A) (p : A -> bool) : list A :=
filter nil p := nil ;
filter (cons a l) p <= p a => {
  filter (cons a l) p true := a :: filter l p ;
  filter (cons a l) p false := filter l p }.

</textarea></div>
<div><p>
By default, equations makes definitions opaque after definition,
    to avoid spurious unfoldings, but this can be reverted on a case by case
    basis, or using the global <tt>Set Equations Transparent</tt> option. </div>
<div><textarea id='coq-ta-9'>
Global Transparent filter.

</textarea></div>
<div><p>
A common use of with clauses is to scrutinize recursive results like the following: </div>
<div><textarea id='coq-ta-10'>

Equations unzip {A B} (l : list (A * B)) : list A * list B :=
unzip nil := (nil, nil) ;
unzip (cons p l) <= unzip l => {
  unzip (cons (pair a b) l) (pair la lb) := (a :: la, b :: lb) }.

</textarea></div>
<div><p>
The real power of with however comes when it is used with dependent types. <p>
<h1>
 Dependent types
</h1>

<p>
   Coq supports writing dependent functions, in other words, it gives the ability to
   make the results <em>type</em> depend on actual <em>values</em>, like the arguments of the function.
   A simple example is given below of a function which decides the equality of two 
   natural numbers, returning a sum type carrying proofs of the equality or disequality 
   of the arguments. The sum type <tt>{ A } + { B }</tt> is a constructive variant of disjunction 
   that can be used in programs to give at the same time a boolean algorithmic information 
   (are we in branch <tt>A</tt> or <tt>B</tt>) and a <em>logical</em> information (a proof witness of <tt>A</tt> or <tt>B</tt>).
   Hence its constructors <tt>left</tt> and <tt>right</tt> take proofs as arguments. The <tt>eq_refl</tt> proof 
   term is the single proof of <tt>x = x</tt> (the <tt>x</tt> is generaly infered automatically).
</div>
<div><textarea id='coq-ta-11'>

Equations equal (n m : nat) : { n = m } + { n <> m } :=
equal O O := left eq_refl ;
equal (S n) (S m) with equal n m := {
  equal (S n) (S ?(n)) (left eq_refl) := left eq_refl ;
  equal (S n) (S m) (right p) := right _ } ;
equal x y := right _.

</textarea></div>
<div><p>
Of particular interest here is the inner program refining the recursive result.
   As <tt>equal n m</tt> is of type <tt>{ n = m } + { n <> m }</tt> we have two cases to consider:
<p>
<ul class="doclist">
  <li> Either we are in the <tt>left p</tt> case, and we know that <tt>p</tt> is a proof of <tt>n = m</tt>,
     in which case we can do a nested match on <tt>p</tt>. The result of matching this equality
     proof is to unify <tt>n</tt> and <tt>m</tt>, hence the left hand side patterns become <tt>S n</tt> and
     <tt>S ?(n)</tt> and the return type of this branch is refined to <tt>{ n = n } + { n <> n }</tt>.
     We can easily provide a proof for the left case. 
<p>
  
  </li>
<li> In the right case, we mark the proof unfilled with an underscore. This will
     generate an obligation for the hole, that can be filled automatically by a 
     predefined tactic or interactively by the user in proof mode (this uses the
     same obligation mechanism as the Program extension
     ). In this case the automatic tactic 
     is able to derive by itself that <tt>n <> m -> S n <> S m</tt>.

</li>
</ul>
   Dependent types are also useful to turn partial functions into total functions by
   restricting their domain. Typically, we can force the list passed to <tt>head</tt> 
   to be non-empty using the specification:
</div>
<div><textarea id='coq-ta-12'>

Equations head {A} (l : list A) (pf : l <> nil) : A :=
head nil pf :=! pf;
head (cons a v) _ := a.

</textarea></div>
<div><p>
We decompose the list and are faced with two cases:
<p>
<ul class="doclist">
  <li> In the first case, the list is empty, hence the proof <tt>pf</tt> of type 
     <tt>nil <> nil</tt> allows us to derive a contradiction. We make use of
     another category of right-hand sides, which we call <em>empty</em> nodes
     to inform the compiler that a contradiction is derivable in this case.
     In general we cannot expect the compiler to find by himself that 
     the context contains a contradiction, as it is undecidable 
     .

  </li>
<li> In the second case, we simply return the head of the list, disregarding
     the proof.

</li>
</ul>
 <p>
<h2>
 Inductive families
</h2>

<p>
   The next step is to make constraints such as non-emptiness part of the 
   datatype itself. This capability is provided through inductive families in
   Coq , which are a similar concept to the generalization 
   of algebraic datatypes to GADTs in functional languages like Haskell 
   . Families provide a way to associate to each constructor 
   a different type, making it possible to give specific information about a value 
   in its type. 
<p>
<h3>
 Equality
</h3>

   The alma mater of inductive families is the propositional equality 
   <tt>eq</tt> defined as: <pre class="inline-coq" data-lang="coq">
Inductive eq (A : Type) (x : A) : A -> Prop := 
 eq_refl : eq A x x. </pre>]]
<p>
   Equality is a polymorphic relation on <tt>A</tt>. (The <tt>Prop</tt> sort (or kind) categorizes
   propositions, while the <tt>Set</tt> sort, equivalent to $$  $$ in Haskell categorizes 
   computational types.) Equality is <em>parameterized</em> by a value <tt>x</tt> of type <tt>A</tt> and 
   <em>indexed</em> by another value of type <tt>A</tt>. Its single constructor states that 
   equality is reflexive, so the only way to build an object of <tt>eq x y</tt> is if 
   <tt>x ~= y</tt>, that is if <tt>x</tt> is definitionaly equal to <tt>y</tt>. 
<p>
   Now what is the elimination principle associated to this inductive family?
   It is the good old Leibniz substitution principle: <pre class="inline-coq" data-lang="coq">
forall (A : Type) (x : A) (P : A -> Type), P x -> forall y : A, x = y -> P y <p>
</pre>
   Provided a proof that <tt>x = y</tt>, we can create on object of type <tt>P y</tt> from an 
   existing object of type <tt>P x</tt>. This substitution principle is enough to show
   that equality is symmetric and transitive. For example we can use 
   pattern-matching on equality proofs to show:
 </div>
<div><textarea id='coq-ta-13'>

Equations eqt {A} (x y z : A) (p : x = y) (q : y = z) : x = z :=
eqt x ?(x) ?(x) eq_refl eq_refl := eq_refl.

</textarea></div>
<div><p>
Let us explain the meaning of the non-linear patterns here that we
   slipped through in the <tt>equal</tt> example. By pattern-matching on the
   equalities, we have unified <tt>x</tt>, <tt>y</tt> and <tt>z</tt>, hence we determined the
   <em>values</em> of the patterns for the variables to be <tt>x</tt>. The <tt>?(x)</tt>
   notation is essentially denoting that the pattern is not a candidate
   for refinement, as it is determined by another pattern. This
   particular patterns are called <quote>inaccessible</quote>.
<p>
<h3>
 Indexed datatypes
</h3>

<p>
   Functions on <tt>vector</tt>s provide more stricking examples of this
   situation.  The <tt>vector</tt> family is indexed by a natural number
   representing the size of the vector: <tt>[ Inductive vector (A : Type) :
   nat -> Type := | Vnil : vector A O | Vcons : A -> forall n : nat,
   vector A n -> vector A (S n) ]</tt>
<p>
   The empty vector <tt>Vnil</tt> has size <tt>O</tt> while the cons operation
   increments the size by one. Now let us define the usual map on
   vectors: </div>
<div><textarea id='coq-ta-14'>
Notation Vnil := Vector.nil.
Notation Vcons := Vector.cons.

Equations vmap {A B} (f : A -> B) {n} (v : vector A n) :
  vector B n :=
vmap f {n:=?(0)} Vnil := Vnil ;
vmap f {n:=?(S n)} (Vcons a n v) := Vcons (f a) (vmap f v).

</textarea></div>
<div><p>
Here the value of the index representing the size of the vector 
   is directly determined by the constructor, hence in the case tree
   we have no need to eliminate <tt>n</tt>. This means in particular that 
   the function <tt>vmap</tt> does not do any computation with <tt>n</tt>, and 
   the argument could be eliminated in the extracted code.
   In other words, it provides only <em>logical</em> information about 
   the shape of <tt>v</tt> but no computational information.
<p>
   The <tt>vmap</tt> function works on every member of the <tt>vector</tt> family,
   but some functions may work only for some subfamilies, for example
   <tt>vtail</tt>:
 </div>
<div><textarea id='coq-ta-15'>

Equations vtail {A n} (v : vector A (S n)) : vector A n :=
vtail (Vcons a n v') := v'.

</textarea></div>
<div><p>
The type of <tt>v</tt> ensures that <tt>vtail</tt> can only be applied to 
   non-empty vectors, moreover the patterns only need to consider 
   constructors that can produce objects in the subfamily <tt>vector A (S n)</tt>,
   excluding <tt>Vnil</tt>. The pattern-matching compiler uses unification 
   with the theory of constructors to discover which cases need to 
   be considered and which are impossible. In this case the failed 
   unification of <tt>0</tt> and <tt>S n</tt> shows that the <tt>Vnil</tt> case is impossible.
   This powerful unification engine running under the hood permits to write
   concise code where all uninteresting cases are handled automatically.
<p>
   Of course the equations and the induction principle are simplified in a 
   similar way. If we encounter a call to <tt>vtail</tt> in a proof, we can 
   use the following elimination principle to simplify both the call and the
   argument which will be automatically substituted by an object of the form
   <tt>Vcons _ _ _</tt>:<pre class="inline-coq" data-lang="coq">
forall P : forall (A : Type) (n : nat), vector A (S n) -> vector A n -> Prop,
(forall (A : Type) (n : nat) (a : A) (v : vector A n), 
  P A n (Vcons a v) v) ->
forall (A : Type) (n : nat) (v : vector A (S n)), P A n v (vtail v) <p>
</pre>
   As a witness of the power of the unification, consider the following function 
   which computes the diagonal of a square matrix of size <tt>n * n</tt>.
</div>
<div><textarea id='coq-ta-16'>

Equations diag {A n} (v : vector (vector A n) n) : vector A n :=
diag {n:=O} Vnil := Vnil ;
diag {n:=(S ?(n))} (Vcons (Vcons a n v) ?(n) v') :=
  Vcons a (diag (vmap vtail v')).

</textarea></div>
<div><p>
Here in the second equation, we know that the elements of the vector 
   are necessarily of size <tt>S n</tt> too, hence we can do a nested refinement
   on the first one to find the first element of the diagonal. <p>
<h2>
 Recursion
</h2>

<p>
  Notice how in the <tt>diag</tt> example above we explicitely pattern-matched
  on the index <tt>n</tt>, even though the <tt>Vnil</tt> and <tt>Vcons</tt> pattern matching
  would have been enough to determine these indices. This is because the
  following definitions fails: </div>
<div><textarea id='coq-ta-17'>

Fail Equations diag' {A n} (v : vector (vector A n) n) : vector A n :=
diag' Vnil := Vnil ;
diag' (Vcons (Vcons a n v) n v') :=
  Vcons a (diag' (vmap vtail v')).

</textarea></div>
<div><p>
Indeed, Coq cannot guess the decreasing argument of this fixpoint
    using its limited syntactic guard criterion: <tt>vmap vtail v'</tt> cannot
    be seen to be a (large) subterm of <tt>v'</tt> using this criterion, even
    if it is clearly <quote>smaller</quote>. In general, it can also be the case that
    the compilation algorithm introduces decorations to the proof term
    that prevent the syntactic guard check from seeing that the
    definition is structurally recursive.
<p>
    To aleviate this problem, <tt>Equations</tt> provides support for
    <em>well-founded</em> recursive definitions which do not rely on syntactic
    checks.
<p>
    The simplest example of this is using the <tt>lt</tt> order on natural numbers
    to define a recursive definition of identity: </div>
<div><textarea id='coq-ta-18'>

Require Import Equations.Subterm.

Equations id (n : nat) : nat :=
  id n by rec n lt :=
  id 0 := 0;
  id (S n') := id n'.

</textarea></div>
<div><p>
Here <tt>id</tt> is defined by well-founded recursion on <tt>lt</tt> on the (only)
    argument <tt>n</tt> using the <tt>by rec</tt> node.  At recursive calls of <tt>id</tt>,
    obligations are generated to show that the arguments effectively
    decrease according to this relation.  Here the proof that <tt>n' < S
    n'</tt> is discharged automatically.
<p>
  Wellfounded recursion on arbitrary dependent families is not as easy
  to use, as in general the relations on families are <em>heterogeneous</em>,
  as the must related inhabitants of potentially different instances of
  the family.  <tt>Equations</tt> provides a <tt>Derive</tt> command to generate the
  subterm relation on any such inductive family and derive the
  well-foundedness of its transitive closure, which is often what's
  required. This provides course-of-values or so-called <quote>mathematical</quote>
  induction on these objects, mimicking the structural recursion
  criterion in the logic. </div>
<div><textarea id='coq-ta-19'>

Derive Signature Subterm for vector.

</textarea></div>
<div><p>
For vectors for example, the relation is defined as: <pre class="inline-coq" data-lang="coq">
Inductive t_direct_subterm (A : Type) :
  forall n n0 : nat, vector A n -> vector A n0 -> Prop :=
    t_direct_subterm_1_1 : forall (h : A) (n : nat) (H : vector A n),
      t_direct_subterm A n (S n) H (Vcons h H) <p>
</pre>
  That is, there is only one recursive subterm, for the subvector
  in the <tt>Vcons</tt> constructor. We also get a proof of:
 </div>
<div><textarea id='coq-ta-20'>

Check well_founded_t_subterm : forall A, WellFounded (t_subterm A).

</textarea></div>
<div><p>
The relation is actually called <tt>t_subterm</tt> as <tt>vector</tt> is just
    a notation for <tt>Vector.t</tt>.
    <tt>t_subterm</tt> itself is the transitive closure of the relation seen as
    an homogeneous one by packing the indices of the family with the
    object itself. Once this is derived, we can use it to define
    recursive definitions on vectors that the guard condition couldn't
    handle. The signature provides a <tt>signature_pack</tt> function to pack a
    vector with its index. The well-founded relation is defined on the
    packed vector type. </div>
<div><textarea id='coq-ta-21'>

Module UnzipVect.
  Context {A B : Type}.

</textarea></div>
<div><p>
We can use the packed relation to do well-founded recursion on the vector.
      Note that we do a recursive call on a substerm of type <tt>vector A n</tt> which
      must be shown smaller than a <tt>vector A (S n)</tt>. They are actually compared
      at the packed type <tt>{ n : nat & vector A n}</tt>. </div>
<div><textarea id='coq-ta-22'>

  Equations unzip {n} (v : vector (A * B) n) : vector A n * vector B n :=
  unzip v by rec (signature_pack v) (@t_subterm (A * B)) :=
  unzip Vnil := (Vnil, Vnil) ;
  unzip (Vector.cons (pair x y) n v) with unzip v := {
    | pair xs ys := (Vector.cons x xs, Vector.cons y ys) }.
End UnzipVect.


</textarea></div>
<div><p>
While this was just mimicking simple structural recursion, we can of
    course use this for more elaborate termination arguments. We put
    ourselves in a section to parameterize a <tt>skip</tt> function by a predicate: </div>
<div><textarea id='coq-ta-23'>

Section Skip.
  Context {A : Type} (p : A -> bool).
  Equations skip_first {n} (v : vector A n) : &{ n : nat & vector A n } :=
  skip_first Vnil := &(0 & Vnil);
  skip_first (Vcons a n v') <= p a => {
                     | true => skip_first v';
                     | false => &(_ & Vcons a v') }.

</textarea></div>
<div><p>
It is relatively straitforward to show that <tt>skip</tt> returns a (large) subvector of its argument </div>
<div><textarea id='coq-ta-24'>

  Lemma skip_first_subterm {n} (v : vector A n) : clos_refl _ (t_subterm _) (skip_first v) &(_ & v).
  Proof.
    funelim (skip_first v).
    constructor 2.
    depelim H.
    constructor 1.
    eapply clos_trans_stepr. simpl.
    apply (t_direct_subterm_1_1 _ _ _ (&(_ & t).2)). apply H.
    rewrite H. constructor. eauto with subterm_relation.
    constructor 2.
  Qed.

End Skip.

</textarea></div>
<div><p>
This function takes an unsorted vector and returns a sorted vector corresponding to it
    starting from its head <tt>a</tt>, removing all elements smaller than <tt>a</tt> and recursing.  </div>
<div><textarea id='coq-ta-25'>

Equations sort {n} (v : vector nat n) : &{n' : _ & vector nat n'} :=
sort v by rec (signature_pack v) (t_subterm nat) :=
sort Vnil := &( _ & Vnil );
sort (Vcons a n v) := let sk := skip_first (fun x => Nat.leb x a) v in &(_ & Vcons a (sort sk.2).2).

</textarea></div>
<div><p>
Here we prove that the recursive call is correct as skip preserves the size of its argument </div>
<div><textarea id='coq-ta-26'>

Next Obligation.
  red. simpl.
  eapply clos_trans_stepr_refl.
  simpl. apply (t_direct_subterm_1_1 _ _ _ (&(_ & v).2)).
  refine (skip_first_subterm _ _).
Qed.

</textarea></div>
<div><p>
To prove it we need a few supporting lemmas, we first write a predicate on vectors
    equivalent to <tt>List.forall</tt>. </div>
<div><textarea id='coq-ta-27'>

Equations forall_vect {A} (p : A -> bool) {n} (v : vector A n) : bool :=
forall_vect _ Vnil := true;
forall_vect p (Vcons x n v) := p x && forall_vect p v.

Require Import Bool.

</textarea></div>
<div><p>
By functional elimination it is easy to prove that this respects the implication
    order on predicates </div>
<div><textarea id='coq-ta-28'>

Lemma forall_vect_impl {A} p p' {n} (v : vector A n)
      (fp : forall x, p x = true -> p' x = true) :
  forall_vect p v = true -> forall_vect p' v = true.
Proof.
  funelim (forall_vect p v). auto.
  simp forall_vect. rewrite !andb_true_iff; intuition auto.
Qed.

</textarea></div>
<div><p>
We now define a simple-minded sorting predicate </div>
<div><textarea id='coq-ta-29'>

Inductive sorted : forall {n}, vector nat n -> Prop :=
| sorted_nil : sorted Vnil
| sorted_cons x n (v : vector nat n) :
    forall_vect (fun y => Nat.leb x y) v = true ->
    sorted v -> sorted (Vcons x v).

</textarea></div>
<div><p>
Again, we show this by repeat functional eliminations. </div>
<div><textarea id='coq-ta-30'>

Lemma fn_sorted n (v : vector nat n) : sorted (sort v).2.
Proof.
  funelim (sort v). </textarea></div>
<div><p>
The first elimination just gives the two <tt>sort</tt> cases. </div>
<div><textarea id='coq-ta-31'>
  - constructor.
  - constructor; auto.
</textarea></div>
<div><p>
Here we have a nested call to skip_first, for which the induction hypothesis holds: <pre class="inline-coq" data-lang="coq">
  H : sorted (sort (skip_first (fun x : nat => x <=? h) t).2).2
  ============================
  forall_vect (fun y : nat => h <=? y) (sort (skip_first (fun x : nat => x <=? h) t).2).2 = true<p>
</pre>
   We can apply functional elimination likewise, even if the predicate argument is instantiated
   here. </div>
<div><textarea id='coq-ta-32'>

  funelim (skip_first (fun x : nat => Nat.leb x h) t); simp sort forall_vect in *; simpl in *.

</textarea></div>
<div><p>
After further simplifications, we get: <pre class="inline-coq" data-lang="coq">
  Heq : (h0 <=? h) = false
  H : sorted (Vcons h0 (sort (skip_first (fun x : nat => x <=? h0) t).2).2)
  ============================
  (h <=? h0) && forall_vect (fun y : nat => h <=? y) (sort (skip_first (fun x : nat => x <=? h0) t).2).2 = true<p>
</pre>
    This requires inversion on the sorted predicate to find out that, by induction,
    <tt>h0</tt> is smaller than all of <tt>fn (skip_first ...)</tt>, and hence <tt>h</tt> is as well.
    This is just regular reasoning. Just note how we got to this point in just
    two invocations of <tt>funelim</tt>. </div>
<div><textarea id='coq-ta-33'>

    depelim H.
    rewrite andb_true_iff.
    enough (h <=? h0 = true). split; auto.
    eapply forall_vect_impl in H.
    apply H.
    intros x h0x. simpl. rewrite Nat.leb_le in *. omega.
    rewrite Nat.leb_le, Nat.leb_nle in *. omega.
Qed.

</textarea></div>
<div><p>
<h3>
 Pattern-matching and axiom K
</h3>
</div>
<div><textarea id='coq-ta-34'>

Module KAxiom.

</textarea></div>
<div><p>
By default we allow the K axiom, but it can be unset. </div>
<div><textarea id='coq-ta-35'>

  Unset Equations WithK.

</textarea></div>
<div><p>
In this case the following definition fails as <tt>K</tt> is not derivable on type <tt>A</tt>. </div>
<div><textarea id='coq-ta-36'>

  Fail Equations K {A} (x : A) (P : x = x -> Type) (p : P eq_refl) (H : x = x) : P H :=
    K x P p eq_refl := p.

</textarea></div>
<div><p>
However, types enjoying a provable instance of the <tt>K</tt> axiom are fine.
      This relies on an instance of the <tt>EqDec</tt> typeclass for natural numbers.
      Note that the computational behavior of this definition on open terms
      is not to reduce to <tt>p</tt> but pattern-matches on the decidable equality proof.
      However the defining equation still holds as a <em>propositional</em> equality. </div>
<div><textarea id='coq-ta-37'>

  Equations K (x : nat) (P : x = x -> Type) (p : P eq_refl) (H : x = x) : P H :=
    K x P p eq_refl := p.

  Print Assumptions K. (* Closed under the global context *)

End KAxiom.
</textarea></div>
<script type="text/javascript">
 var coqdoc_ids = ['coq-ta-2',  'coq-ta-3',  'coq-ta-4', 
                  'coq-ta-5',  'coq-ta-6',  'coq-ta-7',  'coq-ta-8', 
                  'coq-ta-9',  'coq-ta-10',  'coq-ta-11',  'coq-ta-12', 
                  'coq-ta-13',  'coq-ta-14',  'coq-ta-15',  'coq-ta-16', 
                  'coq-ta-17',  'coq-ta-18',  'coq-ta-19',  'coq-ta-20', 
                  'coq-ta-21',  'coq-ta-22',  'coq-ta-23',  'coq-ta-24', 
                  'coq-ta-25',  'coq-ta-26',  'coq-ta-27',  'coq-ta-28', 
                  'coq-ta-29',  'coq-ta-30',  'coq-ta-31',  'coq-ta-32', 
                  'coq-ta-33',  'coq-ta-34',  'coq-ta-35',  'coq-ta-36', 
                  'coq-ta-37'];
</script>
  </div> <!-- /#document     -->
  </div> <!-- /#code-wrapper -->
  </div> <!-- /#ide-wrapper  -->

  <script src="./jscoq/ui-js/jscoq-loader.js" type="text/javascript"></script>
  <script type="text/javascript">
    var coq;

    loadJsCoq('./jscoq/')
              .then(loadJs("./jscoq/ui-external/CodeMirror/addon/runmode/runmode"))
              .then(loadJs("./jscoq/ui-external/CodeMirror/addon/runmode/colorize"))
              .then( function () {
                  var coqInline = document.getElementsByClassName("inline-coq");
                  CodeMirror.colorize(coqInline); })
              .then( function () {
                  coq = new CoqManager (coqdoc_ids,
                                       { base_path: './jscoq/', init_pkgs: ['init', 'equations'] }
                  );
              });
  </script>
</body>
</html>

